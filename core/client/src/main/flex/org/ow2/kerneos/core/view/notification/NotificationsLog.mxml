<?xml version = "1.0" encoding = "utf-8"?>
<!--
     - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     - JASMINe
     - Copyright (C) 2008 Bull S.A.S.
     - Contact: jasmine AT ow2.org
     -
     - This library is free software; you can redistribute it and/or
     - modify it under the terms of the GNU Lesser General Public
     - License as published by the Free Software Foundation; either
     - version 2.1 of the License, or any later version.
     -
     - This library is distributed in the hope that it will be useful,
     - but WITHOUT ANY WARRANTY; without even the implied warranty of
     - MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
     - Lesser General Public License for more details.
     -
     - You should have received a copy of the GNU Lesser General Public
     - License along with this library; if not, write to the Free Software
     - Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307
     - USA
     -
     - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     - $Id$
     - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
-->
<mx:VBox xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:mx="library://ns.adobe.com/flex/mx"
         width = "100%"
         height = "100%"
         creationComplete = "initComponent()">

    <fx:Metadata>
        <![CDATA[
        /**
         * A component that displays Kerneos' notifications log. Allows to clear the list
         * and export it as text. Shows some informations about the Flash player too.
         *
         * @author Julien Nicoulaud
         */
        ]]>
    </fx:Metadata>

    <!-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -->
    <!-- SCRIPT                                                                                  -->
    <!-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -->

    <fx:Script>
        <![CDATA[
        import flash.system.Capabilities;

        import mx.controls.Alert;
        import mx.resources.ResourceManager;
        import mx.collections.ArrayCollection;

        import com.adobe.cairngorm.model.ModelLocator;

        import org.ow2.kerneos.core.vo.KerneosNotification;
        import org.ow2.kerneos.core.model.KerneosModelLocator;
        import org.ow2.kerneos.core.managers.LanguagesManager;

        // =========================================================================
        // Variables
        // =========================================================================

        /**
         * Clear list icon
         */
        [Embed(source="/../assets/clear16.png")]
        [Bindable]
        private var clearIcon:Class;

        /**
         * Copy icon
         */
        [Embed(source="/../assets/copy16.png")]
        [Bindable]
        private var copyIcon:Class;


        // =========================================================================
        // Initialization
        // =========================================================================

        /**
         * Init the component
         */
        private function initComponent(event:Event = null):void {
            // Filter debug messages by default
            KerneosModelLocator.getInstance().notifications.filterFunction = filterDebugMessages;
            KerneosModelLocator.getInstance().notifications.refresh();
        }


        // =========================================================================
        // Public Methods
        // =========================================================================

        /**
         * Export the list as a text and copy it to the system clipboard
         */
        public function copyToClipboard():void {
            // Generate the text and copy it in the clipboard
            System.setClipboard(exportListToText());

            // Display a message
            Alert.show(ResourceManager.getInstance().getString(LanguagesManager.LOCALE_RESOURCE_BUNDLE, 'kerneos.notifications.export.ready-dialog.body'), ResourceManager.getInstance().getString(LanguagesManager.LOCALE_RESOURCE_BUNDLE, 'kerneos.notifications.export.ready-dialog.title'), 4, null, null, copyIcon, 4);
        }


        /**
         * Clear the notifications list
         */
        public function clearList(e:Event = null):void {
            KerneosModelLocator.getInstance().notifications.removeAll()
        }


        // =========================================================================
        // Private Methods
        // =========================================================================

        /**
         * When the "show debug" checkbox changes
         */
        private function handleShowDebugCheckboxChange(event:Event):void {
            if (showDebugCheckBox.selected) {
                KerneosModelLocator.getInstance().notifications.filterFunction = null;
                KerneosModelLocator.getInstance().notifications.refresh();
            }
            else {
                KerneosModelLocator.getInstance().notifications.filterFunction = filterDebugMessages;
                KerneosModelLocator.getInstance().notifications.refresh();
            }
        }


        /**
         * Format the log as text
         */
        private function exportListToText():String {
            var res:String = '================================================================================\n' + KerneosModelLocator.getInstance().application.applicationProject + ' ' + KerneosModelLocator.getInstance().application.applicationName + ', ' + ResourceManager.getInstance().getString(LanguagesManager.LOCALE_RESOURCE_BUNDLE, 'kerneos.notifications.export.header.date', [new Date().toUTCString()]) + '\n' + ResourceManager.getInstance().getString(LanguagesManager.LOCALE_RESOURCE_BUNDLE, 'kerneos.notifications.export.header.flash-player-version', [Capabilities.version + ((Capabilities.isDebugger) ? ' debug' : ' ')]) + ' (' + ResourceManager.getInstance().getString(LanguagesManager.LOCALE_RESOURCE_BUNDLE, 'kerneos.notifications.export.header.flash-player-memory-used', [(System.totalMemory / 1024).toString()]) + ')\n' + '================================================================================\n';

            for each (var notif:KerneosNotification in KerneosModelLocator.getInstance().notifications) {
                res += notif.toString() + "\n";
            }
            return res;
        }


        /**
         * Filters the "debug" level messages on the list
         */
        private function filterDebugMessages(item:Object):Boolean {
            if ((item as KerneosNotification).level == KerneosNotification.DEBUG) {
                return false;
            }
            return true;
        }
        ]]>
    </fx:Script>


    <!-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -->
    <!-- GUI components                                                                          -->
    <!-- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -->

    <!-- The notifications list -->
    <mx:DataGrid id = "notificationsList"
                 width = "100%"
                 height = "100%"
                 dataProvider = "{KerneosModelLocator.getInstance().notifications}"
                 useRollOver = "false"
                 selectable = "false"
                 editable = "false"
                 horizontalScrollPolicy = "on"
                 verticalScrollPolicy = "on"
                 variableRowHeight = "true">

        <mx:columns>
            <mx:DataGridColumn headerText = "{ResourceManager.getInstance().getString(LanguagesManager.LOCALE_RESOURCE_BUNDLE,'kerneos.notifications.table.headers.module')}"
                               dataField = "module"
                               width = "150"
                               itemRenderer = "org.ow2.kerneos.core.view.notification.NotificationsLogModuleRenderer" />
            <mx:DataGridColumn headerText = "{ResourceManager.getInstance().getString(LanguagesManager.LOCALE_RESOURCE_BUNDLE,'kerneos.notifications.table.headers.date')}"
                               dataField = "date"
                               width = "120"
                               itemRenderer = "org.ow2.kerneos.core.view.notification.NotificationsLogDateRenderer" />
            <mx:DataGridColumn headerText = "{ResourceManager.getInstance().getString(LanguagesManager.LOCALE_RESOURCE_BUNDLE,'kerneos.notifications.table.headers.level')}"
                               dataField = "level"
                               itemRenderer = "org.ow2.kerneos.core.view.notification.NotificationsLogLevelRenderer"
                               width = "120" />
            <mx:DataGridColumn headerText = "{ResourceManager.getInstance().getString(LanguagesManager.LOCALE_RESOURCE_BUNDLE,'kerneos.notifications.table.headers.message')}"
                               dataField = "message"
                               wordWrap = "true" />
        </mx:columns>

    </mx:DataGrid>

    <!-- The controls bar -->
    <mx:HBox width = "100%"
             height = "30"
             borderStyle = "outset"
             paddingBottom = "3"
             paddingTop = "3"
             paddingLeft = "4"
             paddingRight = "4"
             verticalAlign = "middle">
        <mx:Button label = "{ResourceManager.getInstance().getString(LanguagesManager.LOCALE_RESOURCE_BUNDLE,'kerneos.notifications.clearButton.label')}"
                   click = "{clearList()}"
                   icon = "{clearIcon}" />
        <mx:Button label = "{ResourceManager.getInstance().getString(LanguagesManager.LOCALE_RESOURCE_BUNDLE,'kerneos.notifications.copyToClipboardButton.label')}"
                   click = "{copyToClipboard()}"
                   icon = "{copyIcon}" />
        <mx:CheckBox id = "showDebugCheckBox"
                     change = "handleShowDebugCheckboxChange(event)"
                     selected = "false" />
        <mx:Text text = "{ResourceManager.getInstance().getString(LanguagesManager.LOCALE_RESOURCE_BUNDLE,'kerneos.notifications.showDebugMessagesButton.label')}"
                 selectable = "false"
                 fontWeight = "bold" />
        <mx:Spacer width = "100%" />
    </mx:HBox>

</mx:VBox>
