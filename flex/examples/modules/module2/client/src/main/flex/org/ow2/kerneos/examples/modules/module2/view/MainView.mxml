<?xml version = "1.0" encoding = "utf-8"?>
<!--
     - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     - Kerneos
     - Copyright (C) 2009-2011 Bull S.A.S.
     - Contact: jasmine AT ow2.org
     -
     - This library is free software; you can redistribute it and/or
     - modify it under the terms of the GNU Lesser General Public
     - License as published by the Free Software Foundation; either
     - version 2.1 of the License, or any later version.
     -
     - This library is distributed in the hope that it will be useful,
     - but WITHOUT ANY WARRANTY; without even the implied warranty of
     - MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
     - Lesser General Public License for more details.
     -
     - You should have received a copy of the GNU Lesser General Public
     - License along with this library; if not, write to the Free Software
     - Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307
     - USA
     -
     - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
-->
<mx:VBox xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:mx="library://ns.adobe.com/flex/mx"
         width="100%"
         height="100%"
         verticalAlign="middle"
         horizontalAlign="center"
         creationComplete="creationCompleteHandler()">

    <fx:Script>
      <![CDATA[
        import com.adobe.cairngorm.Consumer;

        import mx.messaging.events.MessageEvent;
        import mx.messaging.messages.AsyncMessage;

        import org.ow2.kerneos.common.event.ServerSideExceptionEvent;
        import org.ow2.kerneos.examples.modules.module2.event.PostEvent;
        import org.ow2.kerneos.examples.modules.module2.vo.PostVO;

        private var consumer:Consumer = null;

        /**
         * On view creation complete handler
         */
        private function creationCompleteHandler(even:Event = null):void {
            Module2.getInstance().getDispatcher().addEventListener(ServerSideExceptionEvent.SERVER_SIDE_EXCEPTION, ServerSideExceptionEvent.show);

            // Create a asynchronous consumer and subscribe to the topic
            consumer = Module2.getInstance().getServices().getConsumer("module2-serviceAsync");
            consumer.topic = "chat";
            consumer.subscribe();
            consumer.addEventListener(MessageEvent.MESSAGE, postEventHandler);
        }

        /**
         * On Module destroy
         */
        public function destroy():void {
            // Remove all the event listener for avoid memory leaks
            Module2.getInstance().getDispatcher().removeEventListener(ServerSideExceptionEvent.SERVER_SIDE_EXCEPTION, ServerSideExceptionEvent.show);
            if (consumer) {
                consumer.removeEventListener(MessageEvent.MESSAGE, postEventHandler);

                // Unsubscribe the consumer from the topic
                consumer.unsubscribe();
                consumer = null;
            }
        }

        /**
         * A new post handler
         */
        public function postEventHandler(event:MessageEvent):void {
            var msg:AsyncMessage = event.message as AsyncMessage;
            if (msg.body is PostVO) {
                var post:PostVO = msg.body as PostVO;
                textarea.htmlText += "<i>(" + post.date + ")</i><b>" + post.pseudo + ":</b> " + post.message + "<br/>";
            }
        }

        /**
         * Send a post to server
         */
        private function post():void {
            var postEvent:PostEvent = new PostEvent(pseudo.text, message.text);
            Module2.getInstance().getDispatcher().dispatchEvent(postEvent);
        }
        ]]>
    </fx:Script>

    <mx:Form width="100%" height="100%">
        <mx:VBox width="100%" height="100%">
            <mx:TextArea id="textarea" width="100%" height="100%"/>
            <mx:VBox width="100%" height="100">
                <mx:FormItem label="{resourceManager.getString('module2Resources', 'pseudo.label')}">
                    <mx:TextInput id="pseudo"/>
                </mx:FormItem>
                <mx:FormItem label="{resourceManager.getString('module2Resources', 'message.label')}">
                    <mx:TextInput id="message"/>
                    <mx:Button label="{resourceManager.getString('module2Resources', 'post.label')}" click="post()"/>
                </mx:FormItem>
            </mx:VBox>
        </mx:VBox>
    </mx:Form>

</mx:VBox>
